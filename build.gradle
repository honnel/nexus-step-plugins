apply plugin: 'java'
apply plugin: "groovy"
apply plugin: 'idea'
apply plugin: "jacoco"

version = '1.0.1'
defaultTasks 'clean','build'
sourceCompatibility = 1.5
ext.rundeckPluginVersion= '1.1'


configurations{
    //declare custom pluginLibs configuration to include only libs for this plugin
    pluginLibs

    //declare compile to extend from pluginLibs so it inherits the dependencies
    compile{
        extendsFrom pluginLibs
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile group: 'org.rundeck', name: 'rundeck-core', version: '2.1.2'
    compile 'org.codehaus.groovy:groovy-all:2.0.8'

    pluginLibs group: 'log4j', name: 'log4j', version: '1.2.17'
    pluginLibs group: 'commons-codec', name: 'commons-codec', version:'1.9'
    pluginLibs group: 'org.codehaus.groovy.modules.http-builder', name: 'http-builder', version:'0.7'
    testCompile group: 'junit', name:'junit', version: '4.11'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '1.10.19'
    testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3'
    testCompile group: 'com.github.tomakehurst', name: 'wiremock', version: '2.1.6'


}
// task to copy plugin libs to output/lib dir
task copyToLib(type: Copy) {
    into "$buildDir/output/lib"
    from configurations.pluginLibs
}
jacoco {
    toolVersion = "0.7.6.201602180812"
}

jar {
    //include contents of output dir
    from "$buildDir/output"
    manifest {
        attributes 'Rundeck-Plugin-Version': rundeckPluginVersion, 'Rundeck-Plugin-Archive': 'true', 'Rundeck-Plugin-Libs-Load-First':'false'
        //create space-separated list of pluginLibs
        def libList = configurations.pluginLibs.collect{'lib/'+it.name}.join(' ')
        attributes 'Rundeck-Plugin-Classnames': 'com.simplifyops.rundeck.plugin.nexus.NexusDeliverArtifactStepPlugin,com.simplifyops.rundeck.plugin.nexus.NexusExistsArtifactStepPlugin,com.simplifyops.rundeck.plugin.nexus.NexusRegisterArtifactDeliveryStepPlugin', 'Rundeck-Plugin-Libs': "${libList}"
    }
}

//set jar task to depend on copyToLib
jar.dependsOn(copyToLib)

task wrapper(type: Wrapper) {
    gradleVersion = '2.12'
}
